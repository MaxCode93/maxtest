plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

android {
    namespace 'cu.maxwell.firenetstats'
    compileSdk 36

    defaultConfig {
        applicationId = "cu.maxwell.firenetstats"
        minSdk = 24
        targetSdk = 36
        versionCode = 212
        versionName = "2.1.2"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled = true

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }
	
    lint {
        checkReleaseBuilds false
        abortOnError false
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            outputFileName = "fireNetStats-${variant.versionName}-${variant.buildType.name}.apk"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    // Core & UI
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    implementation libs.androidx.constraintlayout
    
    // Lifecycle & Architecture
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.lifecycle.livedata.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.ktx
    
    // Serialization
    implementation libs.gson
    
    // Network & HTTP
    implementation libs.okhttp
    
    // Coroutines
    implementation libs.kotlinx.coroutines.android
    
    // Charts & UI Components
    implementation libs.mpandroidchart
    implementation libs.jspeedtest
    implementation libs.speedviewlib
    implementation libs.androidx.dynamicanimation
    
    // Database
    implementation libs.androidx.room.runtime
    implementation libs.androidx.room.ktx
    kapt libs.androidx.room.compiler
    
    // Testing
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
}
